apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
	compileSdkVersion 22
	buildToolsVersion "23.0.1"

	defaultConfig {
		minSdkVersion 8
		targetSdkVersion 22
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	gradle.projectsEvaluated {
		tasks.withType(JavaCompile) {
			options.compilerArgs << "-Xlint:all"
		}
	}
}

group = "cc.mvdan.accesspoint"
version = "0.1.1"

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_KEY')

	configurations = ['archives']
	dryRun = false
	publish = true
	pkg {
		repo = 'maven'
		name = 'accesspoint'
	}

}

install {
	repositories.mavenInstaller {
		pom {
			project {
				packaging 'aar'
				name 'accesspoint'
				url 'https://github.com/mvdan/accesspoint'
			}
		}
	}
}

task sourcesJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs
	classifier = 'sources'
}

artifacts {
	archives sourcesJar
}
